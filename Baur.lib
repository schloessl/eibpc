//------------------------------------------------------------------------------
// Makros für den EibPC von BAUR
//------------------------------------------------------------------------------
// vim: syntax=enerscript

// Zeitserver für KNX Bus
// @date        26.03.2013
// @version     1
// @author      Stefan Betz
:begin Zeitserver(DatumGA, UhrzeitGA)
:info $Sendet alle 15 Minuten Datum und Uhrzeit auf den KNX Bus.$\\
$Gruppenadresse für Datum (d24)$\\
$Gruppenadresse für Uhrzeit (t24)$
:shortinfo $Zeitserver für Datum/Uhrzeit auf dem KNX Bus$
if (mtime(00,00) or mtime(15,00) or mtime(30,00) or mtime(45,00)) then {
write(DatumGA,setdate());
write(UhrzeitGA,settime());
} endif
:end

// Verbrauchsanzeige
// @date        27.03.2013
// @version     1
// @author      Stefan Betz
:begin Verbrauchsanzeige(VerbrauchButtonID, VerbrauchGA)
:info $Aktualisiert eine Anzeige zum Stromverbrauch (KWh)$\\
$Anzeigeelement$
$Gruppenadresse vom Zähler (KWh)$
:shortinfo $Verbrauchsanzeige (KWh)$
if stime(0) then webdisplay(VerbrauchButtonID, stringformat(VerbrauchGA, 4, 3, 3, 2)+$ KWh$, INFO, INACTIVE, GREEN) endif
:end

// Leistungsanzeige
// @date        27.03.2013
// @version     1
// @author      Stefan Betz
:begin Leistungsanzeige(LeisungButtonID, LeistungGA)
:info $Aktualisiert eine Anzeige zum Stromverbrauch (KW)$\\
$Anzeigeelement$
$Gruppenadresse vom Zähler (KW)$
:shortinfo $Leistungsanzeige (KW)$
if stime(0) then webdisplay(LeistungButtonID, stringformat(LeistungGA, 0, 1, 3, 2)+$ Watt$, INFO, INACTIVE, GREEN) endif
:end

// Verbrauchsmonitor
// @date        26.03.2013
// @version     1
// @author      Stefan Betz
:begin Verbrauchsmonitor(VerbrauchElementID, SeitenElementID, VerbrauchGA)
:info $Füllt die Energieverbrauchsgrafik mit Daten$\\
$Grafik$\\
$Seite$\\
$Gruppenadresse vom Zähler (f32, Wh)$
:shortinfo $Energiemonitor welcher Graphen füllt$
if ((mod(minute(),5u08)==0u08) or (minute()==0u08)) then {
mpchart(VerbrauchElementID, WebZeit, convert(VerbrauchGA,0f16), 0,SeitenElementID);
} endif
:end

// Leistungsmonitor
// @date        26.03.2013
// @version     1
// @author      Stefan Betz
:begin Leistungsmonitor(LeistungElementID, SeitenElementID, LeistungGA)
:info $Füllt die Leistungsgrafik mit Daten$\\
$Grafik$\\
$Seite$\\
$Gruppenadresse vom Zähler (s32, W)$
:shortinfo $Energiemonitor welcher Graphen füllt$
if ((mod(minute(),5u08)==0u08) or (minute()==0u08)) then {
mpchart(LeistungElementID, WebZeit, convert(LeistungGA,0f16), 0,SeitenElementID);
} endif
:end

// Sonnenaufgang
// @date        16.04.2013
// @version     1
// @author      Stefan Betz
:begin Sonnenaufgang(SonnenaufgangButtonID)
:info $Aktualisiert eine Anzeige vom Sonnenaufgang$\\
$Anzeigeelement$
:shortinfo $Sonnenaufgangsanzeige$
if stime(0) then webdisplay(SonnenaufgangButtonID, stringformat(sunrisehour(), 0, 3, 2, 2)+$:$+stringformat(sunriseminute(), 0, 3, 2, 2), LIGHT, INACTIVE, GREEN) endif
:end

// Sonnenuntergang
// @date        16.04.2013
// @version     1
// @author      Stefan Betz
:begin Sonnenuntergang(SonnenuntergangButtonID)
:info $Aktualisiert eine Anzeige vom Sonnenuntergang$\\
$Anzeigeelement$
:shortinfo $Sonnenuntergangsanzeige$
if stime(0) then webdisplay(SonnenuntergangButtonID, stringformat(sunsethour(), 0, 3, 2, 2)+$:$+stringformat(sunsetminute(), 0, 3, 2, 2), NIGHT, INACTIVE, GREEN) endif
:end

// Helligkeitsanzeige
// @date	16.04.2013
// @version	1 
// @author	Stefan Betz
:begin Helligkeitsanzeige(ID, HelligkeitGA)
:info $Eine Statusanzeige für die aktuelle Helligkeit.$ \\
	:$Zahl zwischen 0 und 39 als ID des Elements$\\
	:$Gruppenadresse Licht (f16)$
:shortinfo $Statusanzeige für die aktuelle Helligkeit.$
initGA(HelligkeitGA)
if stime(0) then display(ID,HelligkeitGA,WEATHER,INACTIVE,GREEN) endif
:end

// Windalarmanzeige
// @date	07.11.2013
// @version	1
// @author	Stefan Betz
:begin Windalarmanzeige(ID, WindGA)
:info $Eine Statusanzeige für den Windalarm.$ \\
      :$Zahl zwischen 0 und 39 als ID des Elements$\\
      :$Gruppenadresse Windalarm(b01)$
:shortinfo $Statusanzeige für den Windalarm.$
initGA(WindGA)
if stime(0) and WindGA then display(ID,$Ja$,WIND,DARKRED,GREY) endif
if stime(0) and !WindGA then display(ID,$Nein$,WIND,GREEN,GREEN) endif
:end

// Regenalarmanzeige
// @date	07.11.2013
// @version	1
// @author	Stefan Betz
:begin Regenalarmanzeige(ID, RegenGA)
:info $Eine Statusanzeige für den Regenalarm.$ \\
      :$Zahl zwischen 0 und 39 als ID des Elements$\\
      :$Gruppenadresse Regenalarm(b01)$
:shortinfo $Statusanzeige für den Regenalarm.$
initGA(RegenGA)
if stime(0) and RegenGA then display(ID,$Ja$,WEATHER,STATE6,GREY) endif
if stime(0) and !RegenGA then display(ID,$Nein$,WEATHER,INACTIVE,GREEN) endif
:end

// Windgeschwindigkeitsanzeige
// @date	07.11.2013
// @version	1 
// @author	Stefan Betz
:begin Windgeschwindigkeitsanzeige(ID, WindgeschwindigkeitGA)
:info $Eine Statusanzeige für die aktuelle Windgeschwindigkeit.$ \\
	:$Zahl zwischen 0 und 39 als ID des Elements$\\
	:$Gruppenadresse Licht (f16)$
:shortinfo $Statusanzeige für die aktuelle Windgeschwindigkeit.$
initGA(WindgeschwindigkeitGA)
if change(WindgeschwindigkeitGA) then display(ID,stringformat(WindgeschwindigkeitGA,4,3,3,1)+$ m/s$,WIND,INACTIVE,GREEN) endif
:end

// ZeitUmschaltButton
// @date	23.10.2013
// @version	1
// @author	Stefan Betz
:begin ZeitUmschaltButton(ID, PageID, Name, GA, Stunde, Minute)
Signal^Name = AUS
Signal^Name_Status = Signal^Name
if pbutton(ID,PageID)==1 then {
        Signal^Name = !Signal^Name_Status;
        Signal^Name_Status = Signal^Name
} endif
if Signal^Name == EIN then {
 	pdisplay(ID,$EIN$,CLOCK,ACTIVE,GREEN,PageID) 
} else {
 	pdisplay(ID,$AUS$,CLOCK,INACTIVE,GREY,PageID) 
} endif
if Signal^Name and htime(Stunde, Minute, 00) then write(GA,EIN) endif
:end
